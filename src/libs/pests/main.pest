NEW_LINE = _{ "\n" | "\r\n" | "\r" }

WHITE_SPACE = _{ " " | "\t" }

white_space = @{ WHITE_SPACE+ }

WORD_CHAR = _{ !(NEW_LINE | WHITE_SPACE) ~ ANY }
// TEXT_CHAR = _{ !NEW_LINE ~ ANY }

word = @{ WORD_CHAR+ }

emphasized_inner_word = _{ (!"*" ~ WORD_CHAR)+ }
emphasized_inner = @{ (emphasized_inner_word ~ white_space)* ~ emphasized_inner_word }
emphasized = ${ "*" ~ emphasized_inner ~ "*" }

strong_inner_word = _{ (!"**" ~ WORD_CHAR)+ }
strong_inner = @{ (strong_inner_word ~ white_space)* ~ strong_inner_word }
strong = ${ "**" ~ strong_inner ~ "**" }

line_ne = ${ white_space? ~ (strong | emphasized | word | white_space)+ }
line_e = @{ "" }
line = ${ line_ne | line_e }

paragraph = ${ (line_ne ~ NEW_LINE)* ~ line_ne? }

head1 = ${ "# " ~ line ~ NEW_LINE}
head2 = ${ "## " ~ line ~ NEW_LINE}
head3 = ${ "### " ~ line ~ NEW_LINE}
head4 = ${ "#### " ~ line ~ NEW_LINE}
head5 = ${ "##### " ~ line ~ NEW_LINE}
head6 = ${ "###### " ~ line ~ NEW_LINE}

head = _{ head1 | head2 | head3 | head4 | head5 | head6 }

item = _{ head | paragraph }

file = { SOI ~ (item ~ NEW_LINE)* ~ item? ~ EOI }
